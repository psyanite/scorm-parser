Properties properties = new Properties()
properties.load(project.rootProject.file('local.properties').newDataInputStream())

description 'Parser for SCORM 1.2 packages'
group 'io.github.psyanite'
version '0.2.3'

buildscript {
    repositories {
        jcenter()
    }
    dependencies {
        classpath group: 'com.jfrog.bintray.gradle', name: 'gradle-bintray-plugin', version: '1.7.3'
    }
}

apply plugin: 'maven-publish'
apply plugin: 'com.jfrog.bintray'
apply plugin: 'scala'
apply plugin: 'idea'

repositories {
    mavenCentral()
}

dependencies {
    compile group: 'org.scala-lang',         name: 'scala-library',  version: '2.12.2'
    compile group: 'org.scala-lang.modules', name: 'scala-xml_2.12', version: '1.0.6'

    compile group: 'commons-io',             name: 'commons-io',     version: '2.4'
    compile group: 'net.lingala.zip4j',      name: 'zip4j',          version: '1.3.2'

    testCompile group: 'org.scalatest',      name: 'scalatest_2.12', version: '3.0.1'
    testRuntime 'org.pegdown:pegdown:1.4.2'
}

task sourcesJar(type: Jar, dependsOn: classes) {
    classifier = 'sources'
    from sourceSets.main.allSource
}

task javadocJar(type: Jar, dependsOn: javadoc) {
    classifier = 'javadoc'
    from javadoc.destinationDir
}

artifacts {
    archives sourcesJar
    archives javadocJar
}

def pomConfig = {
    licenses {
        license {
            name "The Apache Software License, Version 2.0"
            url "http://www.apache.org/licenses/LICENSE-2.0.txt"
            distribution "repo"
        }
    }
    developers {
        developer {
            id "psyanite"
            name "psyanite"
            email "psyanite@gmail.com"
        }
    }
}

publishing {
    publications {
        ScormParserPublication(MavenPublication) {
            from components.java
            artifact sourcesJar
            artifact javadocJar
            groupId project.group
            artifactId rootProject.name
            version project.version
            pom.withXml {
                def root = asNode()
                root.appendNode('name', rootProject.name)
                root.appendNode('description', project.description)
                root.appendNode('url', 'https://github.com/psyanite/scorm-parser')
                def scm = root.appendNode('scm')
                scm.appendNode('url', 'https://github.com/psyanite/scorm-parser')
                root.children().last() + pomConfig
            }
        }
    }
}

bintray {
    user = properties.getProperty('bintray.user')
    key = properties.getProperty('bintray.apikey')
    publish = true
    override = true
    publications = ['ScormParserPublication']
    filesSpec {
        from 'build/libs'
    }
    pkg {
        repo = 'psyanite'
        name = rootProject.name
        userOrg = 'psyanite'
        licenses = ['Apache-2.0']
        vcsUrl = 'https://github.com/psyanite/scorm-parser.git'
        version {
            name = project.version
            gpg {
                sign = true
            }
        }
    }
}
